model {
  for(letter in 1:numletters){  
    # numletters = num unique letters with measurements
    # use zeros trick to sample from wrapped-Cauchy distribution
    nll_datamodel[letter] = -log( (1-pow(tau[letterwriter[letter], lettercluster[letter]],2)) / (2*pi_constant*(1+pow(tau[letterwriter[letter], lettercluster[letter]],2)-2*tau[letterwriter[letter], lettercluster[letter]]*cos(pc_wrapped[letter]-mu[letterwriter[letter], lettercluster[letter]]))) ) + C
    zero_vec[letter] ~ dpois( nll_datamodel[letter] )
  }

  # Priors for wrapped cauchy
  for(g in 1:Gsmall){
    # g = cluster
    gamma[g] ~ dgamma(a, b)
    eta[g] ~ dunif(0,2*pi_constant)
    for(w in 1:W){
      # W = num unique writers 
      # use zeros trick to sample from wrapped-Cauchy distribution
      mu[w,g]  ~ dunif(0,2*pi_constant)
      nld_locationparam[w,g] = -log( (1-pow(e,2)) / (2*pi_constant*(1+pow(e,2)-2*e*cos(mu[w,g]-eta[g]))) ) + C
      zero_mat[w,g] ~ dpois(nld_locationparam[w,g])
      tau[w,g] ~ dbeta(c,d)
    }
  }

  for (w in 1:W) {  
    # w = writer
    pi[w,1:G] ~ ddirch(gamma[1:G] + 0.001)
  }

  for(d in 1:D) {                                       
    # d = document
    Y[d,1:G] ~ dmulti(pi[docwriter[d],1:G], docN[d])
  }

  # other values
  C = 30   # for the zeros trick
  pi_constant = 3.14159
  pi_1 = -pi_constant
}
